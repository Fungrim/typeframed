apply plugin: 'license'
apply plugin: 'maven'

buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.10.0'
  }
}  

subprojects {

  apply plugin: 'license'
  apply plugin: 'maven'
  apply plugin: 'java'
  apply plugin: 'eclipse'
  apply plugin: 'signing'
  
  group = 'org.typeframed'
  version = '1.2.2-SNAPSHOT'

  license {
    header rootProject.file('HEADER')
    ext.year = Calendar.getInstance().get(Calendar.YEAR)
    ext.copyHolder = 'Lars J. Nilsson <contact@larsan.net>'
    ignoreFailures = true
  }
  
  signing {
    sign configurations.archives
  }

  task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
  }

  task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
  }

  artifacts {
    archives javadocJar, sourcesJar
  }

  sourceCompatibility = 1.7
  targetCompatibility = 1.7

  repositories {
    mavenCentral()
  }

  ext.libraries = [
    protobufplugin:'ws.antonov.gradle.plugins:gradle-plugin-protobuf:0.9.1',
    protobuf:'com.google.protobuf:protobuf-java:2.5.0',
    guava:'com.google.guava:guava:14.0.1',
    junit:'junit:junit:4.10',
    mockito:'org.mockito:mockito-all:1.9.5',
    parboiled:'org.parboiled:parboiled-java:1.1.4',
    commonslang:'org.apache.commons:commons-lang3:3.1'
  ]

  eclipse {
    classpath {
      defaultOutputDir = file("${projectDir}/build/eclipse")
    }
  }
  
  install {
    repositories.mavenInstaller {
      pom.project {
      
        name 'Typeframed'
        description 'A library for polymorphic protobuf streaming.'
        url 'https://bitbucket.org/fungrim/typeframed'

        scm {
          connection 'scm:hg:https://bitbucket.org/fungrim/typeframed'
          developerConnection 'scm:hg:https://bitbucket.org/fungrim/typeframed'
          url 'https://bitbucket.org/fungrim/typeframed'
        }

        licenses {
          license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'ljn'
            name 'Lars J. Nilsson'
            email 'contact@larsan.net'
          }
        }
      }
    }
  }
  
  uploadArchives {
    repositories {
      mavenDeployer {
    
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
          authentication(userName: ossrhUsername, password: ossrhPassword)
        }

        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
          authentication(userName: ossrhUsername, password: ossrhPassword)
        }

        pom.project {
      
          name 'Typeframed'
          description 'A library for polymorphic protobuf streaming.'
          url 'https://bitbucket.org/fungrim/typeframed'

          scm {
            connection 'scm:hg:https://bitbucket.org/fungrim/typeframed'
            developerConnection 'scm:hg:https://bitbucket.org/fungrim/typeframed'
            url 'https://bitbucket.org/fungrim/typeframed'
          }

          licenses {
            license {
              name 'The Apache Software License, Version 2.0'
              url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
          }

          developers {
            developer {
              id 'ljn'
              name 'Lars J. Nilsson'
              email 'contact@larsan.net'
            }
          }
        }
      }
    }
  }
}
